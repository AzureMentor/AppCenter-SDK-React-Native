apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.properties.get('compileSdkVersion', 26)
    buildToolsVersion rootProject.properties.get('buildToolsVersion', '27.0.3')

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.properties.get('targetSdkVersion', 26)
        versionCode 40
        versionName '1.13.0'
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleDynamicVersion
    implementation ('com.facebook.react:react-native:+') {
        exclude group: 'com.android.support'
    }
    api ('com.microsoft.appcenter:appcenter-push:1.11.4') {
        exclude group: 'com.google.firebase'
    }
    api 'com.google.firebase:firebase-core:16.0.4'
    api 'com.google.firebase:firebase-messaging:17.3.4'

    /*
     * For projects that needs to execute :verifyReleaseResources yet have :lint also succeed,
     * we need to depend on the same support lib version of the app.
     * If we just need :lint to work, excluding the support libs from push/react native is enough,
     * so if ext.supportLibVersion is undefined that should still work for older apps.
     */
    def supportLibVersion = rootProject.properties.get('supportLibVersion');
    if (supportLibVersion != null) {
        api "com.android.support:appcompat-v7:${supportLibVersion}"
        api "com.android.support:support-v4:${supportLibVersion}"
    }

    //api project(':AppCenterReactNativeShared')   // For testing with TestApp
    api 'com.microsoft.appcenter.reactnative:appcenter-react-native:1.13.0'
}
